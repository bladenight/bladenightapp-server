plugins {
    // Call task "shadowJar" to create fat JAR
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'java'
}

group = 'de.greencity.bladenightapp'
version = '0.4-SNAPSHOT'

description = """Bladenight server"""

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven { url "https://maven.geotoolkit.org/" }
    maven { url "https://repo.maven.apache.org/maven2" }
    google()
}

dependencies {
    implementation group: 'org.eclipse.jetty', name: 'jetty-websocket', version:'8.1.18.v20150929'
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.1.18.v20150929'
    implementation group: 'commons-io', name: 'commons-io', version:'2.4'
    implementation group: 'com.google.code.gson', name: 'gson', version:'2.2.2'
    implementation group: 'commons-logging', name: 'commons-logging', version:'1.1.1'
    implementation group: 'joda-time', name: 'joda-time', version:'2.1'
    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version:'1.3.0'
    testImplementation group: 'junit', name: 'junit', version:'4.11'

    implementation project(':bladenightapp-common')
    implementation project(':wampoc')
}

jar {
    manifest {
        attributes 'Main-Class': 'de.greencity.bladenightapp.server.App'
    }
}

task doRelease {
    doLast {
        def buildGradle = new File("build.gradle")
        def matcher = (buildGradle.text =~ /version = '([\d]+)\.([\d]+)-SNAPSHOT'/)
        def major = Integer.parseInt(matcher[0][1])
        def minor = Integer.parseInt(matcher[0][2])

        def oldVersion = "${major}.${minor}"
        def newVersion = "${major}.${minor+1}"

        ant.replace(file: buildGradle, token: "version = '${oldVersion}-SNAPSHOT'", value: "version = '${oldVersion}'")

        exec {
            commandLine "git", "commit", "-a", "-m", "v${oldVersion}"
        }
        exec {
            commandLine "git", "tag", "v${oldVersion}"
        }
        exec {
            commandLine "./gradlew", "shadowJar"
        }

        ant.replace(file: buildGradle, token: "version = '${oldVersion}'", value: "version = '${newVersion}-SNAPSHOT'")

        exec {
            commandLine "git", "commit", "-a", "-m", "Started v${newVersion}"
        }

        exec {
            commandLine "git", "push"
        }

        exec {
            commandLine "git", "push", "--tags"
        }
    }
}

